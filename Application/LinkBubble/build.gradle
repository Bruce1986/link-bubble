buildscript {

    configurations.all {
        resolutionStrategy {
            force 'net.sf.proguard:proguard-gradle:6.0.3'
        }
    }

    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.1'
    }
}

// Manifest version information!
def versionMajor = 2
def versionMinor = 0
def versionPatch = 4
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin:'com.android.application'

boolean isRelease = false;
for (int i = 0; i < project.gradle.startParameter.taskNames.size(); i++)
 {    String item = project.gradle.startParameter.taskNames.get(i);
    if (item.contains("Release")) {
        isRelease = true;
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    namespace 'com.linkbubble'
    compileSdkVersion 34
    buildFeatures {
        buildConfig true
    }
    defaultConfig {
        minSdkVersion 34

        targetSdkVersion 34

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        ndk {
            ldLibs "log"
        }
    }
    flavorDimensions "version"
    productFlavors {
        playstore {
            applicationId = 'com.linkbubble.playstore'

            buildConfigField "String", "STORE_PACKAGE", "\"com.android.vending\""
            buildConfigField "String", "STORE_URL_PREFIX", "\"http://play.google.com/store/apps/details?id=\""
            buildConfigField "String", "STORE_URL", "\"http://play.google.com/store/apps/details?id=com.linkbubble.playstore\""
            buildConfigField "String", "STORE_MY_OTHER_APPS_URL", "\"https://play.google.com/store/apps/developer?id=Link+Bubble\""
            buildConfigField "String", "TAP_PATH_PACKAGE_NAME", "\"com.digitalashes.tappath.playstore\""
        }
    }

    signingConfigs {
        release {
            if (isRelease) {
                storeFile file(System.getenv("LINK_BUBBLE_KEYSTORE_LOCATION"))
                storePassword System.getenv("LINK_BUBBLE_KEYSTORE_PASSWORD")
                keyAlias System.getenv("LINK_BUBBLE_KEY_ALIAS")
                keyPassword System.getenv("LINK_BUBBLE_KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            debuggable true
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/jni/CMakeLists.txt')
        }
    }

    lintOptions {
        lintConfig file("lint.xml")
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation 'com.android.support:palette-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'se.emilsjolander:stickylistheaders:2.1.2'
    implementation('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    implementation 'com.squareup.retrofit:retrofit:1.5.0'
    implementation 'com.timehop.stickyheadersrecyclerview:library:0.4.0@aar'
    implementation 'com.jakewharton:butterknife:7.0.0'
    implementation 'com.squareup:otto:1.3.8'
    implementation files('libs/picasso-2.1.1.jar')
    implementation files('libs/jsoup-1.7.3.jar')
    implementation files('libs/slf4j-android-1.5.8.jar')
}

task showVersion {
    doLast {
        println buildscript.configurations.classpath.resolvedConfiguration.firstLevelModuleDependencies.moduleVersion
    }
}

clean.dependsOn showVersion
